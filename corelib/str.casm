#STR_COMPARE_BEFORE:    0xFF'
#STR_COMPARE_EQUAL:     0x00'
#STR_COMPARE_AFTER:     0x01'

str_size: ( $str -> size )
    0                           ( $str size )

    .loop:
        swap                    ( size $str )

        dup get' 0' =' ?{       ( size $str )
            ( Reached null terminator )
            drop ret
        }

        ++                      ( size $str )
        swap ++                 ( $str size )

        $.loop jump

str_length: ( $str -> length )
    0                           ( $str size )

    .loop:
        swap                    ( size $str )

        dup get'                ( size $str char' )
        
        dup' 0' =' ?{           ( size $str char' )
            ( Reached null terminator )
            drop' drop ret
        }

        0xC0' &' 0x80' =' ?{    ( size $str )
            ( Matches UTF-8 continuation byte, so skip over )
            ++                  ( size $str )
            swap                ( $str size )
            $.loop jump
        }

        ++                      ( size $str )
        swap ++                 ( $str size )

        $.loop jump

str_compare: ( $a $b -> result' )
    over over = ?{
        ( Return equal if addresses are equal )
        drop drop #STR_COMPARE_EQUAL ret
    }

    ${.a: [0] .b: [0]}

    $.b set
    $.a set

    .loop:
        $.a get get'
        $.b get get'

        over' over' 0' =' swap' 0' =' &' ?{
            ( Return equal if reached null terminators of both strings )
            drop' drop' #STR_COMPARE_EQUAL ret
        }

        over' over' <' ?{
            ( $a is alphabetically before $b )
            drop' drop' #STR_COMPARE_BEFORE ret
        }

        >' ?{
            ( $a is alphabetically after $b )
            #STR_COMPARE_AFTER ret
        }

        ( Increment addresses )
        $.a get ++ $.a set
        $.b get ++ $.b set

        $.loop jump

str_eq: ( $a $b -> equal' )
    str_compare #STR_COMPARE_EQUAL =' ret

str_neq: ( $a $b -> notEqual' )
    str_eq 0' =' ret

str=: str_eq ret
str!=: str_neq ret